import org::eclipse::emf::ecore;
import org::applause::applausedsl::applauseDsl;

extension org::eclipse::xtend::util::stdlib::io;
extension templates::MetamodelExtensions;


// Scope bindings
Void scopeStart():
	JAVA templates.Extensions.scopeStart();

Void scopeEnd():
	JAVA templates.Extensions.scopeEnd();

Void scopeBind(EObject obj, String name):
	JAVA templates.Extensions.scopeBind(org.eclipse.emf.ecore.EObject, java.lang.String);

String internalScopeGet(EObject obj):
	JAVA templates.Extensions.scopeGet(org.eclipse.emf.ecore.EObject);

scopeName(EObject obj):
	let name = obj.internalScopeGet() : (name == null ? "<" + obj + " not bound in current scope>" : name);


// Objective C language support

String use(String name):
	use(newProjectClass(name));

String silentUse(String name):
	use(newProjectClass(name)) != null ? "" : "";

String use(ProjectClass projectClass):
	JAVA templates.Extensions.use(org.applause.applausedsl.applauseDsl.ProjectClass);

Void useImportBag(Set[ProjectClass] bag):
	JAVA templates.Extensions.useImportBag(java.util.Set);

objcImports(Collection[String] classNames):
	classNames.toSet().remove(null).remove("").collect(e|"#import \"" + e + '.h";').toString("\n");

create Set[ProjectClass] bagHeaderImports (ProjectClass class):
	((List[ProjectClass]) {}).toSet();

create Set[ProjectClass] bagModuleImports (ProjectClass class):
	((List[ProjectClass]) {}).toSet();

Collection[ProjectClass] withoutBuiltIns(Collection[ProjectClass] classes):
	classes.reject(e|e.name.startsWith("UI") || e.name.startsWith("NS"));

List[ProjectClass] headerImports(ProjectClass class):
	class.bagHeaderImports().withoutBuiltIns().without({class}).sortBy(e|e.name);

List[ProjectClass] moduleImports(ProjectClass class):
	{class, class.bagModuleImports().withoutBuiltIns().without(class.bagHeaderImports()).without({class}).sortBy(e|e.name)}.flatten();

cached ProjectClass newProjectClass(String name):
	JAVA templates.Extensions.createProjectClass(java.lang.String);

h(ProjectClass class):
	useImportBag(class.bagHeaderImports()) -> (class.name + ".h");

m(ProjectClass class):
	useImportBag(class.bagModuleImports()) -> (class.name + ".m");

objc_Call(String target, String methodName, List[String] paramNames, List[String] paramValues):
	"[" + target + " " + methodName + paramNames.objc_MethodParamValues(paramValues) + "]";

objc_Call(String target, String methodName):
	target.objc_Call(methodName, {}, {});

objc_Call(ProjectClass class, String methodName, List[String] paramNames, List[String] paramValues):
	"[" + use(class) + " " + methodName + paramNames.objc_MethodParamValues(paramValues) + "]";

objc_Init(ProjectClass class, List[String] paramNames, List[String] paramValues):
	("[" + use(class) + " alloc]").objc_Call("init", paramNames, paramValues);

objc_Init(ProjectClass class):
	class.objc_Init({}, {});

objc_New(ProjectClass class, List[String] paramNames, List[String] paramValues):
	class.objc_Init(paramNames, paramValues).objc_Call("autorelease");

objc_New(ProjectClass class):
	class.objc_New({}, {});

objc_MethodParamNames(List[String] names):
	names.isEmpty ? {} : {"With" + names.first().toFirstUpper(), names.withoutFirst()}.flatten();

objc_MethodParamSignatures(List[String] names, List[String] types):
	names.isEmpty ? "" :
		(let n = names.objc_MethodParamNames() :
					0.upTo(n.size - 1).collect(i|n.get(i)+ ":(" + types.get(i) + ")" + names.get(i)).toString(" "));

objc_MethodParamValues(List[String] names, List[String] values):
	names.isEmpty ? "" :
		(let n = names.objc_MethodParamNames() :
					0.upTo(n.size - 1).collect(i|n.get(i)+ ":" + values.get(i)).toString(" "));

objc_MethodSignature(ProjectClass class, String modifier, String methodName, String returnType, List[String] paramNames, List[String] paramTypes):
	modifier + " (" + returnType + ") " + methodName + paramNames.objc_MethodParamSignatures(paramTypes);

statements(Collection[String] strings):
	strings.without({null}).collect(e|e + ";").toString("\n");



/* Mapping Model Object -> Objective-C class */

class(View this):
	newProjectClass(this.name.toFirstUpper() + "ViewController");

class(CustomView this):
	this.projectClass;

class(Type this):
	newProjectClass(this.name);

class(TabView view):
	newProjectClass("UITabBarController");


/* Views */

viewFactory(Application app):
	newProjectClass(app.name + "Views");

//TODO: rename Provider -> Method?
objcProviderMethodName(View view):
	"create" + view.name.toFirstUpper();

objcInitSignature(View view):
	view.class().objc_MethodSignature(
		"-", "init", "id",
		view.parameters().name,
		view.parameterTypes());

objcInitCall(View view, List[String] params):
	view.class().objc_Init(view.parameters().name, params);

objcProviderMethodSignature(View view):
	view.class().objc_MethodSignature(
		"+", view.objcProviderMethodName(),
		view.class().use() + " *",
		view.parameters().name,
		view.parameterTypes());

//TODO: potentially confusing to call this also Provider, maybe ViewFactory?
objcProviderMethodCall(ViewCall call, String kvcTarget, String kvcPrefix):
	call.application().viewFactory().objc_Call(
		call.view.objcProviderMethodName(), 
		call.view.parameters().name,
		call.parameters().resolveToProvider());



objcProviderMethodName(ContentProvider this):
	"providerFor" + name.toFirstUpper();




String replaceSettings(String string):
	JAVA templates.Extensions.replaceSettings(java.lang.String);

resolveToValue(Void this):
	"nil";
	
//TODO: special case collection iterator
resolveToValue(ScopeName this):
	this.scopeName() + ".content";

resolveToValue(ScalarExpression this):
	"<resolveToValue is abstract for: " + this + ">";

resolveToValue(StringLiteral this):
	this.value.replaceSettings();

// TODO: use vs. newProjectClass
resolveToValue(StringConcat this):
	'[NSString stringWithFormat:@"' 
		+ values.collect(e|"%@").toString("") + '", '
		+ values.resolveToValue().toString(", ") + "]";

resolveToValue(StringUrlConform this):
	'[' + value.resolveToValue() 
	    + ' stringByAddingPercentEscapesUsingEncoding:NSUTF8StringEncoding]';
	
resolveToValue(StringReplace this):
	'[' + value.resolveToValue() 
	    + ' stringByReplacingOccurrencesOfString:' + match.resolveToValue()
	    + ' withString:' + replacement.resolveToValue() + ']';

resolveToValue(ObjectReference this):
	tail == null ? this.object.resolveToValue()
	             : ( '[' + this.object.resolveToValue() 
	                     + ' valueFor' + ((tail.keyPath().size > 1) ? 'KeyPath:' : 'Key:')
	                     + tail.objcKeyPath() + ']' );





resolveToProvider(ProviderConstruction this): // TODO: rename to ProviderCall?
	"<resolveToProvider is abstract for: " + this + ">";

resolveToProvider(SimpleProviderConstruction this): // TODO: ProviderReference?
	this.expression.resolveToProvider();

// TODO: ComplexProviderConstruction.provider, ViewCall.view are similar -> check similar naming conventions for both

resolveToProvider(ComplexProviderConstruction this): // TODO: ProviderConstructionCall?
	this.application().sharedProviderFactory().objc_Call(
		provider.objcProviderMethodName(),
		provider.parameters().name,
		parameterValues().resolveToProvider()
	);

resolveToProvider(Expression this):
	"<resolveToProvider is abstract for: " + this + ">";
	//TODO: CollectionExpression - CollectionLiteral, CollectionFunction
	//TODO: ScalarExpression - ScalarLiteral, ScalarFunction

resolveToProvider(ObjectReference this):
	this.tail == null
		? object.scopeName()
		: ("ContentProvider+Nested".use() -> 'ContentProvider'.objc_Call(
				'nestedContentProvider',
				{ 'contentProvider', 'keyPath' },
				{ object.scopeName(), tail.objcKeyPath() }));

/* TableView */

constant(TableViewStyle style):
	"UITableViewStyle" + style.name;

constant(CellType type):
	"UITableViewCellStyle" + type.literal;

constant(CellAccessory ac):
	switch(ac.name) {
		case 'Link' : 'UITableViewCellAccessoryDisclosureIndicator'
		case 'Detail' : 'UITableViewCellAccessoryDetailDisclosureButton'
		case 'Check' : 'UITableViewCellAccessoryCheckmark'
		default : 'UITableViewCellAccessoryNone'
	};


index(EObject obj, xpand2::Type type):
	obj.eContainer().eContents().select(e|type.isInstance(e)).indexOf(obj) + 1;
	

idname(Cell cell):
	cell.iterator != null
		? cell.iterator.name
		: ('s' + cell.eContainer().index(Section) + 'c' + cell.index(Cell));

//TOOD: cell is handed in to calculate unique cell selector name for ViewCall - there has to be a better way
action(ViewAction this, Cell cell):
	throwError("action not supported for " + this);

action(ViewCall call, Cell cell):
	'[' + "SelectorAction".use() + ' actionWithObject:self selector:@selector(' + cell.idname() + 'CellSelected:)]';

action(Selector sel, Cell cell):
	'[' + "SelectorAction".use() + ' actionWithObject:self selector:@selector(' + sel.name + ')]';

providers(View view):
	view.parameters();
	
providers(TableView view):
	{ view.parameters(), view.variables }.flatten();

type(TypedName param):
	'ContentProvider'.use() + " *";

ivar(TypedName param):
	"f" + param.name.toFirstUpper();


// Parameter handling

// simulates parameter list, at the moment there can be only 0/1 parameters
// remove with http://ralfebert.lighthouseapp.com/projects/67904/tickets/57-support-for-multiple-parameters
List[Parameter] parameters(View this):
	{ parameter }.remove(null);

List[Parameter] parameters(ContentProvider this):
	{ parameter }.remove(null);

List[Parameter] parameterTypes(View view):
	view.parameters().collect(e|"ContentProvider".use() + " *");

List[ProviderConstruction] parameters(ViewCall view):
	{ view.parameter }.remove(null);

//TODO: parameters vs. argument vs. parameterValues(Bindings?)

List[Expression] parameterValues(ComplexProviderConstruction this):
	{ this.argument }.remove(null);


// Construction of content elements

String constructionStatements(ViewContentElement this, String kvcTarget):
	throwError("constructionStatements not supported for " + this);

cellClass(Cell cell):
	cell.image != null ? use('ImageCell') : use('BoxCell');

constructionStatements(Cell cell, String kvcTarget):
	 cell.cellClass() + ' *cell = [[[' + cell.cellClass() + ' alloc] initWithStyle:' + cell.type.constant() + " reuseIdentifier:nil] autorelease];\n"
	 + cell.text.assignment('cell.textLabel', 'text', kvcTarget)
	 + cell.detailText.assignment('cell.detailTextLabel', 'text', kvcTarget)
	 + cell.image.assignment('cell', 'imageUrl', kvcTarget)
	 + ((cell.accessory.ordinal() == 0 && cell.action != null) ? 'cell.accessoryType = UITableViewCellAccessoryDisclosureIndicator;\n' : "")
	 + ((cell.accessory.ordinal() > 0) ? 'cell.accessoryType = ' + cell.accessory.constant() + ";\n" : "")
	 + ((cell.action != null) ? 'cell.onTouch = ' + action(cell.action, cell) + ";\n" : "")
	 + ((kvcTarget != null) ? 'cell.data = ' + kvcTarget + ';\n' : "[self cell:cell];\n");

constructionStatements(Section section, String kvcTarget):
	 (kvcTarget == null ? '' : use('Section') + ' *section = ')
	 + '[self section' + (section.title == null ? '' : 'WithTitle:' + section.title.expression(kvcTarget, '')) + '];\n'
	 + section.cells.constructionStatements(kvcTarget);


String shortName(ViewContentElement this): "shortName undefined";
String shortName(Cell this): "cell";
String shortName(Section this): "section";

String targetType(ViewContentElement this): "targetType undefined";
String targetType(Cell this): "UITableViewCell";
String targetType(Section this): "Section";


iteratorConstructionStatement(ViewContentElement e, String kvcTarget):
	"[self " + e.shortName() + "s:@selector(" + e.iterator.name + e.shortName().toFirstUpper() + ":) "+
				((kvcTarget == null)
					? "forContentProvider:" + e.iterator.collection.providerExpression()
					: "forList:" + e.iterator.collection.expression(kvcTarget, ''))
			  + "];\n";


constructionStatements(List[ViewContentElement] elements, String kvcTarget):
	elements.collect(e|
		let code = (e.iterator == null ? e.constructionStatements(null) : e.iteratorConstructionStatement(kvcTarget))
			: ((kvcTarget == null && Cell.isInstance(e)) ? "{\n" + code + "}\n" : code)
	 ).toString("\n");




// providerExpression returns the obj c code to resolve an expression
// to a content provider that contains the expression result. This is work
// in progress, currently only straightforward cases are supported.
// See http://applitude.org/#model_expressions

// TODO: replace with resolveToExpression

providerExpression(CollectionExpression this):
	"<providerExpression is abstract for: " + this + ">";

providerExpression(ObjectReference expr):
	expr.tail == null
		? expr.object.providerExpression()
		: silentUse("ContentProvider+Nested") + "[ContentProvider nestedContentProviderWithContentProvider:" + expr.object.providerExpression() +
		                  " keyPath:@\"" + expr.tail.keyPath().toString(".") + "\"]";

providerExpression(ScopeName this):
	"<providerExpression is abstract for: " + this + ">";

providerExpression(TypedName parameter):
	parameter.ivar();





// going straight for the root parameter was a quick hack and is almost
// always wrong -> this is deprecated, will be removed, extensions
// that truly resolve the expression, like expression or providerExpression
// should be used instead

TypedName rootParameter(CollectionExpression this):
	"<ERROR rootParameter not supported for " + this + ">";

TypedName rootParameter(ScalarExpression this):
	"<ERROR rootParameter not supported for " + this + ">";

TypedName rootParameter(StringLiteral expr):
	null;

TypedName rootParameter(ObjectReference ref):
 	ref.object.rootParameter();

TypedName rootParameter(ScopeName this):
	"<ERROR rootParameter not supported for " + this + ">";

TypedName rootParameter(TypedName param):
 	param;

TypedName rootParameter(CollectionIterator it):
 	it.collection.rootParameter();


// If expr goes back to a parameter (=supplied by a content provider), resolve via content provider
// otherwise resolve directly
resolve(ScalarExpression expr):
	expr.rootParameter() != null
		? expr.expression(expr.rootParameter().ivar(), 'content')
		: expr.expression('', '');



assignment(Void expr, String targetObject, String targetProperty, String kvcTarget):
	'';

assignment(ScalarExpression expr, String targetObject, String targetProperty, String kvcTarget):
	targetObject + "." + targetProperty + " = " + expr.expression(kvcTarget, "") + ";\n";

assignment(ObjectReference expr, String targetObject, String targetProperty, String kvcTarget):
	kvcTarget == null ? '[fBindings bind:' + expr.rootParameter().ivar() + ' property:@"' + expr.keyPath('content').toString('.') + '" to:' + targetObject + ' property:@"' + targetProperty + '"];\n'
	                  : targetObject + "." + targetProperty + " = " + expr.expression(kvcTarget, '') + ";\n";



/* Providers */
providerFactory(Application app):
	newProjectClass(app.name + "Providers");

sharedProviderFactory(Application app):
	"[" + app.providerFactory().use() + " sharedProviders]";

applicationDelegate(Application app):
	newProjectClass(app.name + "AppDelegate");

class(ContentProviderImplementation this):
	Void;

class(UrlContentProviderImplementation this):
	newProjectClass("UrlContentProvider");

class(CustomContentProviderImplementation this):
	newProjectClass(this.providerClass.name);

ivar(ContentProvider provider):
	"f" + provider.name.toFirstUpper();




objcType(Type this):
	throwError("<objcType not supported for " + this + ">");

objcType(SimpleType type):
	type.platformType + " *";

objcType(Entity type):
	type.runtimeType ? type.name + " *" : "id";


variable(Type this, String name):
	this.objcType() + (this.objcType().endsWith("*") ? "" : " ") + name;




type(Cell this):
	type.toString().toFirstUpper();
	
firstCell(TableView this):
	sections.first().cells.first();
	
hasBindings(TableView this):
	!this.sections.select(e|e.iterator == null)
	     .cells.select(e|e.iterator == null)
	     .select(e|ObjectReference.isInstance(e.text)
	     || ObjectReference.isInstance(e.detailText)
	     || ObjectReference.isInstance(e.image))
	     .isEmpty;

expression(Expression s, String kvcTarget, String kvcPrefix):
	"<!ERROR!" + s.metaType + ">";
	
expression(CollectionExpression s, String kvcTarget, String kvcPrefix):
	"<!ERROR!" + s.metaType + ">";
	
expression(StringSplit s, String kvcTarget, String kvcPrefix):
	"[" + expression(s.value, kvcTarget, kvcPrefix) + " componentsSeparatedByString: " + expression(s.delimiter, kvcTarget, kvcPrefix) + "]";

expression(ScalarExpression s, String kvcTarget, String kvcPrefix):
	"<!ERROR!" + s.metaType + ">";

expression(StringLiteral this, String kvcTarget, String kvcPrefix):
	this.resolveToValue();
	
expression(StringConcat this, String kvcTarget, String kvcPrefix):
	this.resolveToValue();
		
expression(StringUrlConform this, String kvcTarget, String kvcPrefix):
	this.resolveToValue();
	
expression(StringReplace this, String kvcTarget, String kvcPrefix):
	this.resolveToValue();

List[String] keyPath(ObjectReference this):
	tail == null ? {object.name} : {object.name}.addAll(tail.keyPath());

objcKeyPath(ObjectReference this):
	'@"' + keyPath().toString(".") + '"';

List[String] keyPath(ObjectReference this, String kvcPrefix):
	let head = (kvcPrefix != "" ? {kvcPrefix} : {}):
		head.addAll(keyPath().withoutFirst());

expression(ObjectReference this, String kvcTarget, String kvcPrefix):
	this.resolveToValue();
	
contentProvider(ProviderConstruction this, String kvcTarget, String kvcPrefix):
	"<!ERROR!" + metaType + ">";

contentProvider(SimpleProviderConstruction this, String kvcTarget, String kvcPrefix):
	'[' + use('SimpleContentProvider') + ' providerWithContent:' +
	expression.expression(kvcTarget, kvcPrefix) + ' name:@""]';

contentProvider(ComplexProviderConstruction this, String kvcTarget, String kvcPrefix):
	'[' + this.application().sharedProviderFactory() + ' providerFor' + provider.name + 
		(argument == null ? '' : ': ' + argument.expression(kvcTarget, kvcPrefix) )
	+ ']';
	
methodSignature(ContentProvider p):
	'- (' + use('ContentProvider') + ' *) providerFor' + p.name + (
		p.parameter != null ? ':(id)' + p.parameter.name : '') 
	;
	
allProviders(Application this):
	model().elements.typeSelect(ContentProvider);
	
allViews(Application this):
	model().elements.typeSelect(View);
	
allViewClasses(Application this):
	model().elements.typeSelect(View).removeAll(model().elements.typeSelect(TabView));