// Â© 2011 Ralf Ebert
// Made available under Eclipse Public License v1.0, http://www.eclipse.org/legal/epl-v10.html

application Demo {
	view:Tabs()
}

tabview Tabs {
	tab {
		title: "Inventors"
		view: Inventors(AllInventorsJSON())
		icon: "lightbulb.png"
	}
	tab {
		title: "Reference"
		view: Reference()
		icon: "book.png"
	}
}

type String mapsTo "NSString"

entity Inventor {
	String name
	String imageUrl
	Invention[] inventions
}

entity Invention {
	String name
}

contentprovider AllInventorsJSON stores Inventor[] fetches JSON from "http://applitude.org/demo/inventors.json"
contentprovider AllInventorsXML stores Inventor[] fetches XML from "http://applitude.org/demo/inventors.xml" selects "inventors.inventor"

tableview Inventors(Inventor[] inventors) {
	title: "Inventors"

	section {
		cell Default for inventor in inventors {
			text: inventor.name
			image: inventor.imageUrl
			action: InventorDetail(inventor)
		}
	}
}

tableview InventorDetail(Inventor inventor) {
	title: inventor.name
	style: Grouped

	section {
		cell Value2 {
			text: "Name"
			details: inventor.name
		}
	}

	section {
		title: "Inventions"
		cell Default for invention in inventor.inventions {
			text: invention.name
			action: InventionDetail(invention)
		}
	}
}

tableview InventionDetail(Invention invention) {
	title: invention.name
	style: Grouped
}





tableview Reference {
	title: "Reference"
	titleImage: "applitude.png"

	section {
		title: "Content Providers"
		cell Default {
			text: "JSON"
			action: Inventors(AllInventorsJSON())
		}
		cell Default {
			text: "XML"
			action: Inventors(AllInventorsXML())
		}
	}

	section {
		title: "Tables"
		cell Default {
			text: "Cell styles"
			action: ReferenceCellStyles()
		}
		cell Default {
			text: "cell foreach"
			action: ReferenceCellForeach()
		}
		cell Default {
			text: "section + cell foreach"
			action: ReferenceSectionCellForeach()
		}
		cell Default {
			text: "Error handling"
			action: ReferenceErrorHandling()
		}
		cell Default {
			text: "Custom code hooks"
			action: ReferenceCustomCode()
		}
		cell Default {
			text: "Entity models"
			action: ReferenceEntityModels()
		}
	}

}

tableview ReferenceCellStyles {
	title: "Cell styles"

	section {
		cell Default { text: "Style Default" }
		cell Subtitle { text: "Style" details: "Subtitle" }
		cell Value1 { text: "Style" details: "Value1" }
		cell Value2 { text: "Style" details: "Value2" }
	}

}

tableview ReferenceCellForeach {
	Inventor[] inventors = AllInventorsJSON()

	title: "cell foreach"

	section {
		cell Default for inventor in inventors {
			text: inventor.name
			accessory: Link
			action: InventorDetail(inventor)
		}
	}

}

tableview ReferenceSectionCellForeach {
	Inventor[] inventors = AllInventorsJSON()

	title: "section/cell foreach"

	section for inv in inventors {
		title: inv.name
		cell Default for invention in inv.inventions {
			text: invention.name
			action: InventionDetail(invention)
		}
	}

}

contentprovider Error returns Inventor[] fetches JSON from "http://localhost/none.json"

tableview ReferenceErrorHandling {
	Inventor[] inventors = Error()

	title: "Error handling"
	style: Grouped

	section {
		cell Default for inventor2 in inventors {
			text: inventor2.name
		}
	}

	section for inv in inventors {
		title: inv.name
		cell Default for invention in inv.inventions {
			text: invention.name
			action: InventionDetail(invention)
		}
	}

}

contentprovider Date returns String class DateContentProvider
contentprovider StoredDate stores String class DateContentProvider

customview CustomView class "CustomViewController"

tableview ReferenceCustomCode {
	Inventor[] date = Date()
	Inventor[] storedDate = StoredDate()
	title: "Custom code hooks"
	
	section {
		title: "ContentProvider"
		cell Default { text: date }
	}

	section {
		title: "ContentProvider (stored)"
		cell Default { text: storedDate }
	}
	
	section {
		title: "Custom ViewControllers"
		cell Default {
			text: "Open CustomView"
			action: CustomView()
		}
	}

	section {
		title: "Custom Actions"
		cell Default {
			text: "Open alert"
			action: @selector(openAlertWithCell:)
		}
	}

}


entity Login {
	String username
	String password
}

contentprovider MyLogin returns Login

tableview ReferenceEntityModels {
	Login login = MyLogin()

	style: Grouped
	title: "Login"
	section {
		cell Value1 { text: "Username" details:login.username }
		cell Value1 { text: "Password" details:login.password }
	}
}